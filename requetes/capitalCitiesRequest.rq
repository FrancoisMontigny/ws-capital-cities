SELECT DISTINCT ?capital ?capitalName ?countryName WHERE
{
	?country a dbo:Country.
	?country dbo:flag ?flag.
	{
		{?country dbo:capital ?capital}
		UNION
		{?country dbp:capital ?capital}
	}

        ?country rdfs:label ?countryName. 
	?country <http://purl.org/linguistics/gold/hypernym> ?type.
        FILTER(lang(?countryName) = 'en')

        ?capital rdfs:label ?capitalName
	FILTER (lang(?capitalName) = 'en') 

	# Ensure it's the actual capital of an actual country
	FILTER (?type = dbr:State || ?type = dbr:Country || ?type = dbr:Republic || ?type = dbr:Kingdom || 
	?type = dbr:Democracy || ?type = dbr:Enclave || ?type = dbr:Microstate || ?type = dbr:Monarchy || ?type = dbr:Nation || ?type = dbr:United)
	FILTER NOT EXISTS {?country dbo:dissolutionYear ?year}
	FILTER NOT EXISTS {?country dbo:dissolutionDate ?date}
	FILTER NOT EXISTS {?capital a dbo:Person}

}
ORDER BY ?capital
